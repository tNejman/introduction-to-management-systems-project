{
  "cells": [
    {
      "cell_type": "code",
      "execution_count": 50,
      "metadata": {
        "colab": {
          "base_uri": "https://localhost:8080/"
        },
        "id": "pge5-zOZx2st",
        "outputId": "27302608-59ec-4b7b-aac2-4c814e485dd4"
      },
      "outputs": [
        {
          "name": "stdout",
          "output_type": "stream",
          "text": [
            "Using default Community Edition License for Colab. Get yours at: https://ampl.com/ce\n",
            "Licensed to AMPL Community Edition License for the AMPL Model Colaboratory (https://ampl.com/colab).\n"
          ]
        }
      ],
      "source": [
        "%pip install -q amplpy\n",
        "from amplpy import AMPL, ampl_notebook\n",
        "\n",
        "ampl = ampl_notebook(\n",
        "    modules=[\"highs\"],\n",
        "    license_uuid=\"default\",\n",
        ")"
      ]
    },
    {
      "cell_type": "code",
      "execution_count": null,
      "metadata": {
        "colab": {
          "base_uri": "https://localhost:8080/"
        },
        "id": "ORRy4jUbx5QL",
        "outputId": "a6430590-6ebd-4ccd-f062-0f3d72e029c7"
      },
      "outputs": [
        {
          "name": "stdout",
          "output_type": "stream",
          "text": [
            "Overwriting wsyz_projekt.mod\n"
          ]
        }
      ],
      "source": [
        "%%writefile wsyz_projekt.mod\n",
        "\n",
        "set PRODUCENCI;\n",
        "set MAGAZYNY;\n",
        "set SKLEPY;\n",
        "set WARZYWA;\n",
        "param HORYZONT;\n",
        "set TYGODNIE := 1..HORYZONT;\n",
        "\n",
        "param KosztTransportuTonakm;\n",
        "\n",
        "param ProdukcjaMax{PRODUCENCI, WARZYWA} >= 0;\n",
        "param PojemnoscMagazynuCentralnego{MAGAZYNY} >= 0;\n",
        "param OdlegloscPM{PRODUCENCI, MAGAZYNY} >= 0;\n",
        "\n",
        "param SprzedazBazowa{SKLEPY, WARZYWA} >= 0;\n",
        "\n",
        "# Definicja sezonowości\n",
        "param MnoznikSezonowy{t in TYGODNIE} :=\n",
        "    if t <= 13 then 1.3\n",
        "    else if t <= 26 then 0.8\n",
        "    else if t <= 39 then 0.6\n",
        "    else 1.1;\n",
        "\n",
        "# Sprzedaż prognozowana\n",
        "param SprzedazPrognozowana{s in SKLEPY, w in WARZYWA, t in TYGODNIE}\n",
        "    := SprzedazBazowa[s,w] * MnoznikSezonowy[t];\n",
        "\n",
        "# Średnia tygodniowa\n",
        "param SredniaSprzedaz{s in SKLEPY, w in WARZYWA}\n",
        "    := (sum{t in TYGODNIE} SprzedazPrognozowana[s,w,t]) / HORYZONT;\n",
        "\n",
        "# Limity magazynowe\n",
        "param PojemnoscSklepu{s in SKLEPY, w in WARZYWA} := 2 * SredniaSprzedaz[s,w];\n",
        "param ZapasMin{s in SKLEPY, w in WARZYWA} := 0.1 * SredniaSprzedaz[s,w];\n",
        "\n",
        "param OdlegloscMS{MAGAZYNY, SKLEPY} >= 0;\n",
        "\n",
        "var x_pm{PRODUCENCI, MAGAZYNY, WARZYWA} >= 0;\n",
        "var x_ms{MAGAZYNY, SKLEPY, WARZYWA, TYGODNIE} >= 0;\n",
        "var zapas_s{SKLEPY, WARZYWA, 0..HORYZONT} >= 0;\n",
        "\n",
        "minimize KosztCalkowity:\n",
        "    sum{p in PRODUCENCI, m in MAGAZYNY, w in WARZYWA}\n",
        "        x_pm[p,m,w] * OdlegloscPM[p,m] * KosztTransportuTonakm +\n",
        "    sum{m in MAGAZYNY, s in SKLEPY, w in WARZYWA, t in TYGODNIE}\n",
        "        x_ms[m,s,w,t] * OdlegloscMS[m,s] * KosztTransportuTonakm;\n",
        "\n",
        "subject to LimitProdukcji {p in PRODUCENCI, w in WARZYWA}:\n",
        "    sum{m in MAGAZYNY} x_pm[p,m,w] <= ProdukcjaMax[p,w];\n",
        "\n",
        "subject to LimitPojemnosciMC {m in MAGAZYNY}:\n",
        "    sum{p in PRODUCENCI, w in WARZYWA} x_pm[p,m,w] <= PojemnoscMagazynuCentralnego[m];\n",
        "\n",
        "subject to BilansMagazynuCentralnego {m in MAGAZYNY, w in WARZYWA}:\n",
        "    sum{p in PRODUCENCI} x_pm[p,m,w] = sum{s in SKLEPY, t in TYGODNIE} x_ms[m,s,w,t];\n",
        "\n",
        "subject to BilansSklepu {s in SKLEPY, w in WARZYWA, t in TYGODNIE}:\n",
        "    zapas_s[s,w,t] = zapas_s[s,w,t-1] + sum{m in MAGAZYNY} x_ms[m,s,w,t] - SprzedazPrognozowana[s,w,t];\n",
        "\n",
        "subject to PojemnoscMagazynuSklepu {s in SKLEPY, w in WARZYWA, t in TYGODNIE}:\n",
        "    zapas_s[s,w,t] <= PojemnoscSklepu[s,w];\n",
        "\n",
        "subject to MinimalnyZapasSklepu {s in SKLEPY, w in WARZYWA, t in TYGODNIE}:\n",
        "    zapas_s[s,w,t] >= ZapasMin[s,w];\n",
        "\n",
        "subject to ZapasPoczatkowy {s in SKLEPY, w in WARZYWA}:\n",
        "    zapas_s[s,w,0] = ZapasMin[s,w];"
      ]
    },
    {
      "cell_type": "code",
      "execution_count": 52,
      "metadata": {
        "colab": {
          "base_uri": "https://localhost:8080/"
        },
        "id": "lrVZ2pKKx6WF",
        "outputId": "e32cebeb-4d85-4f85-9df8-ff9e4268a3c7"
      },
      "outputs": [
        {
          "name": "stdout",
          "output_type": "stream",
          "text": [
            "Overwriting wsyz_projekt.dat\n"
          ]
        }
      ],
      "source": [
        "%%writefile wsyz_projekt.dat\n",
        "\n",
        "set PRODUCENCI :=\n",
        "  P1  # Błonie\n",
        "  P2  # Książenice\n",
        "  P3  # Góra Kalwaria\n",
        "  P4  # Otwock\n",
        "  P5  # Wołomin\n",
        "  P6; # Legionowo\n",
        "\n",
        "set MAGAZYNY :=\n",
        "  M1  # Pruszków\n",
        "  M2  # Piaseczno\n",
        "  M3; # Zielonka\n",
        "\n",
        "set WARZYWA :=\n",
        "  Ziemniaki\n",
        "  Kapusta\n",
        "  Buraki\n",
        "  Marchew;\n",
        "\n",
        "set SKLEPY :=\n",
        "  S1    # Erazma Ciołka 25, 01-445 Warszawa\n",
        "  S2    # Wolska 64, 01-134 Warszawa\n",
        "  S3    # Gościnna 37, Blizne Łaszczyńskiego\n",
        "  S4    # Pirenejska 2c, 01-493 Warszawa\n",
        "  S5    # Zygmunta Modzelewskiego 54B, 02-679 Warszawa\n",
        "  S6    # Puławska 69, 02-595 Warszawa\n",
        "  S7    # Miedziana 3a/1c, 00-814 Warszawa\n",
        "  S8    # Dobra 54, 00-341 Warszawa\n",
        "  S9    # Wrzesińska 4/Lok. U2 i U3, 03-713 Warszawa\n",
        "  S10;  # Żupnicza 23, 03-821 Warszawa\n",
        "\n",
        "param HORYZONT := 52;\n",
        "param KosztTransportuTonakm := 5;\n",
        "\n",
        "param ProdukcjaMax: Ziemniaki Kapusta Buraki Marchew :=\n",
        "                P1  160       180     120    160\n",
        "                P2  90        100     140    80\n",
        "                P3  160       70      190    100\n",
        "                P4  260       50      60     100\n",
        "                P5  200       230     190    110\n",
        "                P6  250       130     80     100;\n",
        "\n",
        "param PojemnoscMagazynuCentralnego :=\n",
        "                M1 800\n",
        "                M2 1200\n",
        "                M3 750;\n",
        "\n",
        "# Na podstawie drogi proponowanej przez Google Maps\n",
        "param OdlegloscPM:  M1         M2          M3 :=\n",
        "                P1  17         39          44\n",
        "                P2  18         29          54\n",
        "                P3  42         20          44\n",
        "                P4  37         28          25\n",
        "                P5  42         38          9\n",
        "                P6  38         43          25;\n",
        "\n",
        "param OdlegloscMS: S1 S2 S3 S4 S5 S6 S7 S8 S9 S10 :=\n",
        "                M1 16 15 14 15 15 18 15 22 23 22\n",
        "                M2 26 24 26 29 12 14 17 21 22 24\n",
        "                M3 23 23 25 22 20 18 16 20 12 14;\n",
        "\n",
        "# Bazowa sprzedaż - każdy sklep ma inne realne zapotrzebowanie\n",
        "param SprzedazBazowa: Ziemniaki Kapusta Buraki Marchew :=\n",
        "  S1  1.2  0.7  0.4  0.8\n",
        "  S2  1.0  0.5  0.3  0.6\n",
        "  S3  0.5  0.3  0.2  0.4\n",
        "  S4  0.9  0.6  0.4  0.7\n",
        "  S5  2.0  1.2  0.8  1.5\n",
        "  S6  1.8  1.0  0.7  1.3\n",
        "  S7  0.8  0.4  0.3  0.5\n",
        "  S8  0.7  0.4  0.2  0.5\n",
        "  S9  1.1  0.6  0.4  0.8\n",
        "  S10 1.6  0.9  0.6  1.2;"
      ]
    },
    {
      "cell_type": "code",
      "execution_count": 54,
      "metadata": {
        "colab": {
          "base_uri": "https://localhost:8080/"
        },
        "id": "lrS302kdx8N1",
        "outputId": "c8fc8368-ab61-4047-afcb-24abce56dee1"
      },
      "outputs": [
        {
          "name": "stdout",
          "output_type": "stream",
          "text": [
            "HiGHS 1.11.0: \b\b\b\b\b\b\b\b\b\b\b\b\b\bHiGHS 1.11.0: optimal solution; objective 221613.5\n",
            "4124 simplex iterations\n",
            "0 barrier iterations\n",
            "\n",
            "\"option abs_boundtol 2.220446049250313e-16;\"\n",
            "or \"option rel_boundtol 1.947759692324836e-16;\"\n",
            "will change deduced dual values.\n",
            "\n",
            "KosztCalkowity = 221614\n",
            "\n",
            "--- DECYZJE A: TRANSPORT ROCZNY P -> M ---\n",
            "x_pm [*,M1,*]\n",
            ":   Buraki Kapusta  Marchew Ziemniaki    :=\n",
            "P1   22.42   96.04   160      160\n",
            "P2    0       0       40.02    53.04\n",
            "P3    0       0        0        0\n",
            "P4    0       0        0        0\n",
            "P5    0       0        0        0\n",
            "P6    0       0        0        0\n",
            "\n",
            " [*,M2,*]\n",
            ":  Buraki Kapusta Marchew Ziemniaki    :=\n",
            "P1    0       0        0        0\n",
            "P2    0       0        0        0\n",
            "P3    0       0      100      160\n",
            "P4    0       0        0        0\n",
            "P5    0       0        0        0\n",
            "P6    0       0        0        0\n",
            "\n",
            " [*,M3,*]\n",
            ":  Buraki Kapusta Marchew Ziemniaki    :=\n",
            "P1     0       0       0        0\n",
            "P2     0       0       0        0\n",
            "P3     0       0       0        0\n",
            "P4     0       0       0        0\n",
            "P5   190     230     110      200\n",
            "P6     0       0       0        0\n",
            ";\n",
            "\n",
            "--- DECYZJE B: TRANSPORT TYGODNIOWY M -> S (TYDZIEN 1) ---\n",
            "x_ms[m,s,w,1] [M1,*,*]\n",
            ":   Buraki  Kapusta  Marchew Ziemniaki    :=\n",
            "S1    0      0.91     2.484     1.56\n",
            "S10   0      0        0         0\n",
            "S2    0.78   0.65     0.78      1.3\n",
            "S3    0.26   0.9315   0.52      0.65\n",
            "S4    0      0.78     0.91      1.17\n",
            "S5    0      0        0         0\n",
            "S6    0      0        0         0\n",
            "S7    0      0        0.65      0\n",
            "S8    0      0        1.5525    0\n",
            "S9    0      0        0         0\n",
            "\n",
            " [M2,*,*]\n",
            ":   Buraki Kapusta Marchew Ziemniaki    :=\n",
            "S1     0       0     0        0\n",
            "S10    0       0     0        0\n",
            "S2     0       0     0        0\n",
            "S3     0       0     0        0\n",
            "S4     0       0     0        0\n",
            "S5     0       0     1.95     6.21\n",
            "S6     0       0     1.69     2.34\n",
            "S7     0       0     0        0\n",
            "S8     0       0     0        0\n",
            "S9     0       0     0        0\n",
            "\n",
            " [M3,*,*]\n",
            ":   Buraki Kapusta Marchew Ziemniaki    :=\n",
            "S1    1.04   0       0       0\n",
            "S10   0.78   1.17    3.726   2.08\n",
            "S2    0      0       0       0\n",
            "S3    0      0       0       0\n",
            "S4    0.52   0       0       0\n",
            "S5    1.04   1.56    0       0\n",
            "S6    1.82   3.105   0       0\n",
            "S7    0.39   1.04    0       1.04\n",
            "S8    0.52   0.52    0       2.1735\n",
            "S9    1.04   1.56    1.04    1.43\n",
            ";\n",
            "\n",
            "--- DECYZJE C: STAN ZAPASÓW W SKLEPACH (KONIEC TYGODNIA 1) ---\n",
            "zapas_s[s,w,1] [*,*]\n",
            ":     Buraki  Kapusta  Marchew Ziemniaki    :=\n",
            "S1    0.558    0.0665   1.52     0.114\n",
            "S10   0.057    0.0855   2.28     0.152\n",
            "S2    0.4185   0.0475   0.057    0.095\n",
            "S3    0.019    0.57     0.038    0.0475\n",
            "S4    0.038    0.057    0.0665   0.0855\n",
            "S5    0.076    0.114    0.1425   3.8\n",
            "S6    0.9765   1.9      0.1235   0.171\n",
            "S7    0.0285   0.558    0.0475   0.076\n",
            "S8    0.279    0.038    0.95     1.33\n",
            "S9    0.558    0.837    0.076    0.1045\n",
            ";\n",
            "\n"
          ]
        }
      ],
      "source": [
        "%%ampl_eval\n",
        "reset;\n",
        "model wsyz_projekt.mod;\n",
        "data wsyz_projekt.dat;\n",
        "option solver highs;\n",
        "solve;\n",
        "\n",
        "display KosztCalkowity;\n",
        "\n",
        "# Transport roczny od producentów do magazynów\n",
        "print \"--- DECYZJE A: TRANSPORT ROCZNY P -> M ---\";\n",
        "display x_pm;\n",
        "\n",
        "# Transport tygodniowy z Magazynów do Sklepów na przykładzie 1 tygodnia\n",
        "print \"--- DECYZJE B: TRANSPORT TYGODNIOWY M -> S (TYDZIEN 1) ---\";\n",
        "display {m in MAGAZYNY, s in SKLEPY, w in WARZYWA} x_ms[m, s, w, 1];\n",
        "\n",
        "# Stan zapasów w sklepach na koniec 1 tygodnia\n",
        "print \"--- DECYZJE C: STAN ZAPASÓW W SKLEPACH (KONIEC TYGODNIA 1) ---\";\n",
        "display {s in SKLEPY, w in WARZYWA} zapas_s[s, w, 1];\n"
      ]
    }
  ],
  "metadata": {
    "colab": {
      "provenance": []
    },
    "kernelspec": {
      "display_name": "Python 3",
      "name": "python3"
    },
    "language_info": {
      "name": "python"
    }
  },
  "nbformat": 4,
  "nbformat_minor": 0
}
